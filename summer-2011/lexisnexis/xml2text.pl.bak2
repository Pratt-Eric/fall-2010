#!/usr/bin/perl -w
# @file xml2text.pl. 
# @brief convert the xml input file into text format, in this process the
# footnotes, headnotes and the citation metadata will be extracted.
# @func getLNI(), getLCitations(), getSCitations(), extractText(). 
# @author Vinayak & Simon Guo. 
# @revision 04/10/2011 created by Vinayak. 
# @revision 06/16/2011 updated comments by Simon Guo. 
# @revision 06/23/2011 added sentence cutting part by Simon Guo. 
# @revision 06/27/2011 added detailed comments. 
# This file has been archived, please do not update it. 
use Lingua::EN::Sentence qw( get_sentences add_acronyms );

# Counting the number of documents processed. 
my $cnt = 1;

# ======================================================================
# @brief this is the main loop over all the legal documents. It will go 
# over all input legal issue documents(usually, each line is a legal doc)
# and extract footnotes, headnotes, citation metadata and paragraph text.
# 
# In order to use the power of distributed computing such as hadoop, I 
# used <STDIN> as input to the program and <STDOUT> as output file. 
# 
# @input <STDIN>, each line of input is an issue document in xml format. 
# @output <STDOUT>, the information we need will include footnote, headnote, 
# citation meta-data and paragraph/sentences. When these info is printed 
# to STDOUT, a unique identifier will be prepended to the string. They are
# FOOTNOTES:, HEADNOTES:, CITATION. And we can process the data according 
# to these info for the following steps. 
# 
# @var Global variables : 
# $lnistr - the doc unique doc as string; 
# $docstr - courtcase representation string; 
# %metastr - variable for storing metadata as string; 
# meta string will be of the form as:
# key  <--> value. 
# S_10 <--> State Nat'l Bank v. Farah
# 
# @comments Due to the large size of the input legal document, it is 
# advisible to pass reference to function by reference rather than by 
# value. And $lnistr is treated as a global variable such that all the 
# functions in the script can use it.
# ======================================================================
MAINLOOP:while (<STDIN>) {
    # a global string variable. 
    our $docstr = $_; 
    our $lnistr = "";

    # %metastr is used to store metadata so that every time when I do 
    # text extraction and sentence cutting, I can remove the hassle of 
    # dealing with the large number of abbreviations within the string. 
    # And then change back the string that was replaced by a label, 
    # labels in this project include ((|S|L|HN|FN|)_[0-9]+).
    our %metastr = ();

    # ==============================
    # Extract text and meta-data. 
    # ==============================
    # get lni string for the doc. 
    &getLNI();

    # only process representation and opinions part. 
    if ($_ =~ /<courtcase:representation>(.*?)<\/courtcase:representation>/){
	$docstr = $1; 
    }
    if ($_ =~ /<courtcase:opinions[^>]*>(.*)<\/courtcase:opinions>/) {
	$docstr .= " " . $1; 
    }

    # Preprocessing of the document, mainly correct the case citation problems
    # 1, if no lni is provided, we need to go over the xml doc and search local
    #    the same local id and resolve the lni. 
    # 2, if no info is given as what type of citation it is, we can analyze the 
    #    context info and get make a decision. 
    &preProcessXml(); 

    # Look for all link citations with lni's involved.
    &getCCitations();
#=begin DEBUG

    # Look for statutory citations.
    &getSCitations();
    
    # Look for headnotes. 
    &getHeadnotes(); 

    # Look for footnodes. 
    &getFootnotes();

    # Get text from the document. 
    &getText();
#=end DEBUG
#=cut
    $cnt++;
}

# ==================================================
# @brief Preprocess the xml document, mainly correct
# errors and unresolved issues. 
# @param the global document will be used. 
# @return none. 
# ==================================================
sub preProcessXml {
    # Eliminate noise and unstandard characters.
    $docstr =~ s/Â //g;
    $docstr =~ s/\$ /\$/g;
    $docstr =~ s/\&amp\;/\&/g;		# transform ascii chars to normal form. 

    # correct the unresolved lni citation. 

}

# ==================================================
# @brief Get the LNI string from the document. This value is constant
# throughout the document. 
# @param global variable $docstr will be used.
# @return The document LNI string. 
# ==================================================
sub getLNI {
    if ($docstr =~ '<lncr:persistentidentifier>(.*?)<\/lncr:persistentidentifier>')
    {
	$lnistr = $1;
	$lnistr =~ s/\-//g;
    }
    return;
}

=begin citation
    Determinination of case citation is dependent on the normprotocol property
    of the citation, if normprotocol="lexsee", citation is a case citation, else
    if normprotocol ="lexstat", citation is a statutory citation. But there are
    errors within the input xml files, such as what should we do if there is no
    normprotocol property, how do we handle it? 
=end citation
=cut 


# ==================================================
# @brief This routine helps in finding the Case citations. That is "A vs. B"
# kind of citations. This captures and records casereftokens in the
# file. And the format it captures is as follows:
# Lni-current document::CC_(citation_number)::TokenID::LNI-target document:: \
# Actual citation 
# @param $docstr  which are global variables. 
# @param lnistr which is the global variable.
# @return case citation strings in the input string.
# ==================================================
sub getCCitations {
    my $i = 1; 
    my $citeid; 
    my $citestr; 
    my $citecontent;
    my $localciteid; 
    my $destlni; 

    print "\nCase Citations\n";
    # need to verify the structure of original xml file. 
    LOOP:while ($docstr =~ /((.{200})(<lnci:cite ID=\"([0-9A-Z]+)\"[^>]*?normprotocol=(\"lexsee\")?[^>]*>(.*?)<\/lnci:cite>))/g) {
	$destlni = $2;
	$citestr = $3; 
	$localciteid = $4; 

	# if no type is provided, we need to guess based on the content. 
	my $type = $5;
	if ((!$type) && ($citestr !~ m/<lnci:text txt=\"v\.\"[^>]*>/)) {
	    next LOOP; 
	}

	$citeid = "CC_$i";
	$docstr =~ s/\Q$citestr/ $citeid /g; 
	# some citation doesn't contain destination lni string. 
	if ($destlni =~ m/lni=\"([A-Z0-9\-]+)\"/g) {
	    $destlni = $1; 
	    $destlni =~ s/-//g;
	} else {
	    $destlni = "UNRESOLVED";
	}

    	# get cite content. 
	$citestr =~ s/<[^>]+>/ /g;
	$citestr =~ s/  +/ /g;
	$citestr =~ s/^ +//g;
	$citestr =~ s/  +([,])/,/g; # remove space in front of comma etc. 

	$metastr{$citeid} = $citestr; 

    	# print "CASEREFS:$citeid:\t" . $lnistr . ":$localciteid" . "::$destlni:$citestr\n";
    	print "CASEREFS:" . $lnistr . ":$citeid" . "::$destlni:$citestr\n";
	$i++;
    }

    return;
}

# ==================================================
# @brief As compared with the above routine, this routine helps in finding
# statutory citations in the document. The format it captures is as follows:
# Lni-current document::S_(citation_number)::localID::citation string. 
# When extracting statutory citaions, we need to consider cases that might 
# contain citations. Currently, I have identified three cases. 
# case 1: <lnci:cite ... normprotocol="lexstat"; 
# case 2: <ref:cite4thisresource...>; 
# case 3: <lnci:cite ...>. which does not contain the normprotocol property. 
# @param $docstr which are global variables for each doc. 
# @param lnistr - the document unique string. 
# @return none, but will update the global $docstr. 
# ==================================================
sub getSCitations {
    my $i = 1; 
    my $localid = ""; 
    my $citestr; 
    my $citeid;

    print "\nStatutory Citations\n";

    # Process citations in head of doc. 
    # xml node is: <ref:cite4thisresource...>
    # added by simon on July 05, 2011. 
    LOOP:while ($docstr =~ m/((<lnci:cite ID=\"([A-Z0-9]+)\"[^>]*normprotocol=(\"lexstat\")?[^>]*>)(.*?)<\/lnci:cite>)/g) {
	$citestr = $1;
	$localid = $3; 
	my $type = $4; 
	if (! $localid) {
	    $localid = "UNDETERMINED";
	}

	# TODO, try to determine the type of citation. 
	if ((! $type) && ($citestr =~ m/<lnci:text txt=\"v\.\"[^>]*>/)) {
	    next LOOP;
	}

	$citeid = "SC_$i";

	$docstr =~ s/\Q$citestr/ $citeid /g; 

	$citestr =~ s/<[^>]+>/ /g; 
	$citestr =~ s/  +/ /g; 
	$citestr =~ s/^ +//g;
	# print "CASEREFS:$citeid:\t" . "$lnistr:$localid" . "::$citestr\n";
	print "CASEREFS:" . "$lnistr:$citeid" . "::$citestr\n";

	$metastr{$citeid} = $citestr; 
	$i++; 
    }

    return; 
}

# ==================================================
# @brief extract headnotes from document.
# @param $docstr, a global variable. 
# @return none. 
# ==================================================
sub getHeadnotes {
    my $i = 1;
    my $hnoteid;

    print "\nHeadnote:\n";

    # extract headnote tag. 
    while ($docstr =~ /(<casesum:headnote[^>]+>(.*?)<\/casesum:headnote>)/g) {
	my $headnotestr = $1; 
	$hnoteid = "HN_$i";
	$docstr =~ s/\Q$headnotestr/ $hnoteid /g; 

	if ($headnotestr =~ /(<text>)(.*?)(<\/text>)/) {
	    my $hnstr = $2;
	    $hnstr =~ s/<.*?>//g;
	    $hnstr =~ s/<\/.*?>//g;
	    $metastr{$hnoteid} = $hnstr;
	    print "HEADOUTPUT:" . "$lnistr:$hnoteid:$hnstr\n\n";
	    $i++;
	}
    }

    return; 
}

# ==================================================
# @brief extract footnotes from document.
# @param $doc, a global variable. 
# @return none. 
# ==================================================
sub getFootnotes {
    my $fnoteid; 
    my $i = 1; 

    print "\nFootnote:\n";

    # Look for all the footnotes within the document.
    while ($docstr =~ /((<footnote>)(.*?)(<\/footnote>))/g) {
	my $fnotestr = $1; 
	$fnoteid = "FN_$i"; 
	# get footnote id from anchor. 
	# if ($fnotestr =~ m/<ref:anchor id=\"(fn_fnote[0-9]+)\"/) {
	#     $fnoteid=$1; 
	# }
	$metastr{$fnoteid} = "EMPTY";
	$docstr =~ s/\Q$fnotestr/$fnoteid /g;

	if ($fnotestr =~ /(<text>)(.*?)(<\/text>)/)	{
	    $fnote = $2;
	    $fnote =~ s/<[^>]+>//g; 
	    $fnote =~ s/  +/ /g;
	    $fnote =~ s/\( +/\(/g;
	    
	    if ( $fnote =~ / \" / ) {
		$fnote =~ s/ \" ([^"]+\S)\"/ \"$1\"/g ;
		$fnote =~ s/ \"(\S[^"]+) \" / \"$1\" /g ;
		$fnote =~ s/ \" ([^"]+) \" / \"$1\" /g ;
	    }
	    $metastr{$fnoteid} = $fnote;
	    # print $metastr{$fnoteid} . "\n";

	    print "FOOTOUTPUT:" . "$lnistr:$fnoteid:$fnote\n\n";
	    $i++;
	}
    }
    
    return;
}

# ==================================================
# @brief extract text from the document. 
# NOTE: paragraphs like clspara_[0-9]+ will be ignored here.
# @param $docstr, the global document string. 
# @return none. 
# ==================================================
sub getText {
    my $id = 1; 
    my $parstr = ""; 
    print $lnistr . "\n\n"; 
    my %pars = (); 		# paragraph table. 

    print "\nText:\n";

    # paragraphs embedded within paragraph can be identified by
    # <\/?blockquote>. Let's remove them first. 
    while ($docstr =~ m/(<blockquote[^>]*>(.*?)<\/blockquote>)/g) {
	my $quotestr = $1;
	$parstr = $2; 

	# get paragraph id.
	if ($parstr =~ m/id=\"para_([0-9]+)\"/) {
	    $id = int($1);
	    $pars{$id} = $parstr; 
	    # Then remove the embedded quota paragraph. 
	    $docstr =~ s/\Q$quotestr/ para_$id /;
	} else {
	    $docstr =~ s/\Q$quotestr/ /;
	}
    }

    # extract all the paragraph data. 
    while($docstr =~ m/(<p>(.*?)<\/p>)/g) {
	$parstr = $1; 

	# get paragraph id.
	if ($parstr =~ m/id=\"para_([0-9]+)\"/) {
	    $id = int($1);
	    $pars{$id} = $parstr; 
	}

	$parstr =~ s/\Q$parstr/ /g; # remove embeded par. 
    }

    # Process the paragraphs, should be sort by order. 
    for my $key (sort { $a <=> $b } keys %pars) {
	&processParagraph($pars{$key}); 
    }

    return ;
}

# ==================================================
# @brief This routine is used to process the paragraph 
# text, including getting paragraph ids and and the 
# text as string. 
# @param a <p> and </p> bounded pargraph xml string. 
# @return true/false; and print out the paragraph data
# into standard output. 
# ==================================================
sub processParagraph {
    my $doc = $_[0];
    my $i=1;
    my $parid = "zzz"; 
    $parstr = "";

    if ($doc =~ /<ref:anchor id=\"([a-z]+_[0-9]+)\"\/>/) {
	$parid = $1;
	print ">>>>>>>>>>Start of $parid>>>>>>>>>>\n";
	print $parid . "\n";
    }
    
    # extract paragraph data. 
    if($doc =~ m/<text>(.*)<\/text>/) {
	$parstr = $1;
	
	$parstr =~ s/<[^>]+>//g; # remove xml labels. 
	$parstr =~ s/  +/ /g; # remove additional space. 
	$parstr =~ s/ ([,\;\.])/$1/g; # remove space before ending punctuations.
	$parstr =~ s/^ +//g; # remove leading space. 
	
	print $parstr . "\n\n";
	
	# do sentence cutting. 
	&preProcess(); 
	&postProcess(); 
    }
    print "\n<<<<<<<<<<End of $parid<<<<<<<<<<\n\n";
    return; 
}

# ==================================================
# @brief This process will be used to remove all the
# errors that can cause the failure of the sentence 
# cutting program. 
# @param $parstr The paragraph string. 
# @return None.
# ==================================================
sub preProcess {
    $_ = $parstr;
    chomp;
    # ==================================================
    # Correct errors and abnormal forms of the input. 
    # ==================================================
    # rule R_0; handling citation case names, -- casename.pl 
    # rule example: in file -> casename.example. 

    # rule R_1; abbrevation handling. -- abbrev.pl 
    # rule example: in file -> abbrev.example. 
    s/([0-9a-z]+\."?)([A-Z])/$1 $2/g; # Add space between sentences. 

    # rule R_2; labels handling. -- label.pl
    # rule example: in file -> label.example.
    s/(\w+)\. (((FN|HN|L|S)_[0-9]+ )+)([A-Z])/$1, $2\. $5/g; # adjust "xxx. FN_10 Foo "
    s/([\.\?]\") (((FN|HN|L|S)_[0-9]+ )+)([A-Z])/$1, $2\. $5/g; # adjust "xxx." FN_10 Foo "

    s/ ([,\?\.])/$1/g;		      # remove space in front of [,?].

    # ==================================================
    # Add acronyms within docs. 
    # ==================================================
    add_acronyms('ab', 'tit', 'pen', 'supp', 'bhd', 'indus'); 

    # because acronyms are capital initialized words some may not work such as seq. 
    s/(seq\.)/$1,/g;
    s/(cert\.)/$1,/g;
    s/(disc\.)/$1,/g;
    s/(etc\.) ([A-Z])/$1. $2/g;	# ended with etc. might have problem. 
    
    # special cases.
    s/"If/" If/g;
    s/Id\.//g;			# This term is redundant?

    # remove the numbering at the beginning of paragraph. 
    s/\. ([SL]_[0-9]+)/\, $1/g;	# e.g: xxx. S_10. => xxx, S_10.
    s/\" ([SL]_[0-9]+)/", $1/g;
    s/([SL]_[0-9]+)\. ?([^A-Z])/$1\, $2/g; # change . to ,

    # Adjust headnotes and footnotes. 
    s/(FN_[0-9]+) ([A-Z])/$1\. $2/g;
    s/\. ([HF]N_[0-9]+)/\, $1/g;   
    s/([SL]_[0-9]+[.,;!'"])/$1 /g; # e.g: S_10,Abc => S_10, Abc

    # adjust marks. like .... ... etc. 
    s/\.\.\.\.?( [^A-Z])/, $1/g; # e.g: .... by => , by
    s/\.\.\.\.?( [A-Z])/\. $1/g; # e.g: .... By => . By
    s/\.\.\.?\.?([^ ])/\.$1/g;	 # e.g: ..., by => ., by

    # adjust numbers. 
    s/^[0-9]+\. //g;		# e.g: 1. The extent of ....
    s/([\"\.]) ([0-9]+[^\.])/$1, $2/g;
    s/([\"\.]) ([\(])/$1, $2/g;		   # e.g: A. (2nd) => A., (2nd)
    s/([\",\.][0-9]+)\. ([^A-Z])/$1, $2/g; # e.g: "2. xxx. => "2, xxx.
    return; 
}

# ==================================================
# @brief sentence cutting using perl sentence module. 
# @param none, actually it will use the package use 
# the global variable called $parstr. 
# @return none. 
# ==================================================
sub postProcess {
    my $cnt = 1; 
    # &loadAbbrev(); 
    my $sentences = get_sentences($parstr); 
    foreach $sentence (@$sentences) {
	# post-process the sentence.
	$sentence =~ s/  +/ /g;	# remove redundant spaces. 
	$sentence =~ s/\.\./\./g; # remove additional period mark. 
	$sentence =~ s/ ,/,/g;	  # remove space before ,. 
	$sentence =~ s/^\s+//g;	  # remove leading space. 
	$sentence =~ s/^\"([^\"]+)/$1/g; # remove unbalanced quotation. 

	# If sentence doesn't contain ending mark, add one. 
	if (($sentence =~ m/(^.*)([^\.\"\?\:])$/) && 
	    ($sentence !~ m/para_[0-9]+/)) {
	    $sentence = $1 . $2 . "."; 
	}
	print "$cnt:  " . $sentence . "\n"; 
	$cnt++;
    }
    return; 
}

# ==================================================
# @brief load abbrevations from file. 
# @param none. 
# @return none. 
# ==================================================
sub loadAbbrev {
    my $cnt = 0; 
    open(ABBFILE, "abbrev.abb");

    LOOP:while (<ABBFILE>) {
	chomp; 
	s/[0-9]+ +//g; 		# remove counting.
	# print $_ . " ";
	push @abbv, "$_"; 
	$cnt++;
	if ($cnt >= 600) {
	    last LOOP;
	}
    }
    add_acronyms(@abbv);

    close ABBFILE; 
    return; 
}
